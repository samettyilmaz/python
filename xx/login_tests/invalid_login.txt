*** Settings ***
Documentation     A test suite containing tests related to invalid login.
...
...               These tests are data-driven by they nature. They use a single
...               keyword, specified with Test Template setting, that is called
...               with different arguments to cover different scenarios.
...
...               This suite also demonstrates using setups and teardowns in
...               different levels.
Suite Setup       Open Browser To Login Page
Suite Teardown    Close Browser
Test Setup        Go To Login Page
Test Template     Login With Invalid Credentials Should Fail
Resource          resource.txt

*** Test Cases ***    User Name        Password

Invalid Username      invalid          ${VALID PASSWORD}
	[Tags]   Regression   Dashboard

Invalid Password      ${VALID USER}    invalid
	[Tags]   Regression   Dashboard
Invalid Username And Password   invalid          whatever
	[Tags]   Regression   Dashboard
Empty Username        ${EMPTY}         ${VALID PASSWORD}
	[Tags]   Regression   Dashboard
Empty Password        ${VALID USER}    ${EMPTY}
	[Tags]   Regression   Dashboard
Empty Username And Password   ${EMPTY}         ${EMPTY}
	[Tags]   Regression   Dashboard


*** Keywords ***
Login With Invalid Credentials Should Fail
    [Arguments]    ${username}    ${password}
    Input Username    ${username}
    Input Password    ${password}
    Submit Credentials
    Login Should Have Failed

Login Should Have Failed
    Page Should Contain    Sorry, could not find any user with the given username / password.
